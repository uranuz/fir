expr {=
	def cls {=
		def.pos el;
		def.body noscope {%
			expr "e-" ~ el ~ " ";
			if not{=empty instanceName} "i-" ~ instanceName ~ " ";
			if not{=empty cssBaseClass} cssBaseClass ~ " ";
			if {=has {=scope} "cssClass"} and not{=empty cssClass} cssClass ~ " ";
		}
	};

	def firScopeOpts {=
		def.kv
			fields: [
				'instanceName', 'cssBaseClass', 'cssClass'
			];
		def.body noscope {=
			var
				result: {}
				allScope: {=scope};
			for name in fields {=
				if {=has allScope name} {=
					setat result {=at allScope name} name;
				}
			};
			expr result;
		}
	};

	def firScopeOptsEnc {=
		def.kv
			fields: [
				'instanceName', 'cssBaseClass', 'cssClass'
			];
		def.body noscope {=
			toJSONBase64 {=firScopeOpts fields: fields}
		}
	};

	def rsFieldFormat {=
		def.pos rsOrRec, fieldName;
		def.body {=
			at rsOrRec.format {=at rsOrRec.namesMapping fieldName}
		}
	};

	def rsGet {=
		def.pos rs, fieldName, rowIndex;
		def.body {=
			var record: {=at rs rowIndex};
			at record fieldName;
		}
	};

	def recGet {=
		def.pos record, fieldName;
		def.body {=at record fieldName}
	};

	def recGetStr {=
		def.pos record, fieldName;
		def.body {=
			var
				fmt: {=rsFieldFormat record fieldName},
				value: {=recGet record fieldName },
				result: null;
			if {=has fmt "enum" } {=
				for enumPair in fmt.enum {=
					if enumPair.v == value {=
						if {=has enumPair "n"} {=
							set result: enumPair.n;
						} else {=
							set result: enumPair.v;
						}
					}
				}
			} else {=
				set result: value;
			};
			expr result;
		}
	};

	def enumWithNull {
		def.pos
			list: null
			nullName: ''
			nullValue: null;
		def.body {=
			insert list {'n': nullName, 'v': nullValue} 0
		}
	}
}